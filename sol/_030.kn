func main()
	var n: int :: cui@inputInt()
	var k: int :: cui@inputInt()
	
	var isPrime: []bool :: sieve(n)
	var a: []int :: #[n + 1]int
	for i(2, n)
		if(isPrime[i])
			var j: int :: i
			while(j <= n)
				do a[j] :+ 1
				do j :+ i
			end while
		end if
	end for
	
	var ans: int :: 0
	for i(2, n)
		if(a[i] >= k)
			do ans :+ 1
		end if
	end for
	
	do cui@print("\{ans}\n")
	
	func sieve(n: int): []bool
		if(n < 2)
			ret[false].repeat(n + 1)
		end if
		var isPrime: []bool :: [true].repeat(n + 1)
		do isPrime[0] :: false
		do isPrime[1] :: false
		for i(2, n)
			if(isPrime[i])
				var j: int :: i * i
				while(j <= n)
					do isPrime[j] :: false
					do j :+ i
				end while
			end if
		end for
		ret isPrime
	end func
end func
