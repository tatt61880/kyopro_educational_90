func main()
	const mod: int :: 1000000007
	var n: int :: cui@inputInt()
	var b: int :: cui@inputInt()
	var k: int :: cui@inputInt()
	
	var c: []int :: #[k]int
	for i(0, k - 1)
		do c[i] :: cui@inputInt()
	end for
	
	var pow10: []int :: #[64]int
	do pow10[1] :: 10 % b
	for ib(2, 63)
		do pow10[ib] :: (pow10[ib - 1] ^ 2) % b
	end for
	
	var dp: []int :: #[b]int
	for ik(0, k - 1)
		do dp[c[ik] % b] :+ 1
	end for
	
	var ans: []int :: #[b]int
	do ans[0] :: 1
	var ib: int :: 0
	while(n > 0)
		if(n % 2 = 1)
			var ansNext: []int :: #[b]int
			for i(0, b - 1)
				for j(0, b - 1)
					do ansNext[(i * pow10[ib + 1] + j) % b] :+ ans[i] * dp[j]
					do ansNext[(i * pow10[ib + 1] + j) % b] :% mod
				end for
			end for
			do ans :: ansNext
		end if
		block
			var dpNext: []int :: #[b]int
			for i(0, b - 1)
				for j(0, b - 1)
					do dpNext[(i * pow10[ib + 1] + j) % b] :+ dp[i] * dp[j]
					do dpNext[(i * pow10[ib + 1] + j) % b] :% mod
				end for
			end for
			do dp :: dpNext
		end block
		do n :/ 2
		do ib :+ 1
	end while
	do cui@print("\{ans[0]}\n")
end func
